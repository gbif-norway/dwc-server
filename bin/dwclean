#!/usr/bin/perl

use strict;
use warnings;
use utf8;
use 5.14.0;
use open qw/:std :utf8/;

use Getopt::Std;
use Parse::CSV;

use DwC;

package GBIFNorway;

our %names = ();
our %filters = ();
our %cleaners = ();

for my $format (glob 'formats/*.pl') {
  do $format;
}

package main;

# fiks dette + lag man
our $usage = <<EOS;
$0  [-hmruv]
    [-b basisofrecord]
    [-c collectioncode]
    [-d modified]
    [-f format]
    [-i institutioncode]
    [-k kingdom]
    [-x verbatim]
EOS

our $default_f = "dwc";

our ($opt_b, $opt_c, $opt_d, $opt_e, $opt_f, $opt_g, $opt_h, $opt_i);
our ($opt_k, $opt_m, $opt_r, $opt_s, $opt_u, $opt_v, $opt_x);
getopts("k:x:s:i:c:d:e:f:ghruvmb:");

if($opt_h) {
  print($usage) and exit;
}

our $verbatim;
if($opt_x) {
  open $verbatim, ">", $opt_x;
  say $verbatim join("\t", @DwC::terms);
}

say STDOUT join("\t", @DwC::terms);

my $filter = $GBIFNorway::filters{$opt_f || $default_f};
my $n = $GBIFNorway::names{$opt_f || $default_f};

my %options = (
  handle => \*STDIN,
  filter => $filter,
  binary => 1,
  sep_char => "\t",
  quote_char => undef,
  escape_char => undef,
  encoding_in  => "utf8"
);

# stygg hack
if ($GBIFNorway::names{$opt_f || $default_f}) {
  $options{names} = $GBIFNorway::names{$opt_f || $default_f},
}

my $csv = Parse::CSV->new(%options);

my %uniques;
my $errno = 0;

my $nn = 0;

while (my $raw = $csv->fetch) {
  my $dwc = DwC->new($raw, $csv->row);

  # Set values provided explicitly
  $$dwc{basisOfRecord} = $opt_b if($opt_b);
  $$dwc{institutionCode} = $opt_i if($opt_i);
  $$dwc{collectionCode} = $opt_c if($opt_c);
  $$dwc{catalogNumber} = $nn++ if($opt_m);
  $$dwc{modified} = $opt_d if($opt_d);

  # Print verbatim record
  $dwc->printcsv($verbatim, \@DwC::terms) if($opt_x);

  # Preliminary cleanup
  my $cleaner = $GBIFNorway::cleaners{$opt_f || $default_f};
  &$cleaner($dwc);

  # Perform all plugin-based validations
  for my $plugin ($dwc->plugins) {
    $plugin->validate($dwc);
  }

  # Check for duplicate ids
  if(!$opt_u) {
    my $catalogNumber = $$dwc{catalogNumber} || "";
    if($uniques{$catalogNumber}) {
      $dwc->log("error", "Duplicate catalogNumber", "core");
    }
    $uniques{$catalogNumber} = 1;
  }

  # Need a relatively stable id~
  my $id;
  if($$dwc{occurrenceID}) {
    $id = $$dwc{occurrenceID};
  } else {
    $id = "urn:catalog:" . $dwc->triplet;
    $$dwc{occurrenceID} = $id;
  }

  # Print errors, warnings, and info messages
  my $row = $$dwc{_row};
  for my $error (@{$$dwc{error}}) {
    $errno++;
    say STDERR "$errno\terror\t$id\t$row\t$$error[0]\t$$error[1]";
  }
  for my $warning (@{$$dwc{warning}}) {
    $errno++;
    say STDERR "$errno\twarning\t$id\t$row\t$$warning[0]\t$$warning[1]";
  }
  if($opt_v) {
    for my $info (@{$$dwc{info}}) {
      $errno++;
      say STDERR "$errno\tinfo\t$id\t$row\t$$info[0]\t$$info[1]";
    }
  }

  # Skip the record if there are fatal errors
  next if (@{$$dwc{errors}});

  # Print it!
  $dwc->printcsv(\*STDOUT, \@DwC::terms);
}

# Die ASAP on CSV errors
if($csv->errstr) {
  die($csv->errstr . "\n");
}

# Clean up...
close($verbatim) if $verbatim;

